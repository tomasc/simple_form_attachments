#= require numeraljs

# https://github.com/zenorocha/jquery-boilerplate
(($, window) ->
  pluginName = 'simple_form_attachments'
  document = window.document

  defaults =
    debug: false
    sortable_options:
      handle: '.<%= SimpleFormAttachments.dom_class(:attachment, :col, :handle) %>'
      axis: 'y'
      items: '> .<%= SimpleFormAttachments.dom_class(:attachment) %>'
      forcePlaceholderSize: true
      placeholder: '<%= SimpleFormAttachments.dom_class(:attachment, :placeholder) %>'
      containment: 'parent'

    slide_speed: 'fast'

  class Plugin
    constructor: (@element, options) ->
      @options = $.extend {}, defaults, options

      @$element = $(@element)

      @_defaults = defaults
      @_name = pluginName

      @init()
      @attachments_counter = 0

    init: ->
      @set_unique_ids()

      @get_file_input().on 'fileuploadalways', (e) => @init_sortable() if @is_sortable()
      @init_sortable() if @is_sortable()
      @init_fileupload()

    init_sortable: () ->
      @get_attachment_list().sortable(@options.sortable_options)#.disableSelection()

    init_fileupload: () ->
      console.log "init jQuery.fileupload" if @options.debug
      @get_file_input().fileupload
        dataType: 'json'
        dropZone: @$element
        formData: {
          'attachment_type]': @get_attachment_type_input().val(),
          'attachment_parent[name]': @get_attachment_parent_name_input().val(),
          'attachment_parent[class]': @get_attachment_parent_class_input().val(),
          'attachment_relation[type]': @get_attachment_relation_type_input().val(),
          'attachment_relation[key]': @get_attachment_relation_key_input().val(),
          'attachment_relation[name]': @get_attachment_relation_name_input().val(),
          'attachment_relation[referenced]': @get_attachment_relation_referenced_input().val(),
          'authenticity_token': $('meta[name="csrf-token"]').attr('content')
          'attachment_relation[multiple]': @is_multiple()
        }
        namespace: 'simple_form_attachments'
        url: @get_attachments_path()
        # getNumberOfFiles: -> 2
        # maxNumberOfFiles: 2

        start: (e, data) =>
          @set_logs(e, data) if @options.debug
          @disable_form_submit()

        add: (e, data) =>
          @set_logs(e, data) if @options.debug
          @get_attachment_list().find('.<%= SimpleFormAttachments.dom_class(:attachment) %>').remove() unless @is_multiple()

          for file in data.files
            properties = {
              size: (file.size)
              size_human: @get_size_human(file.size)
              mime_type: file.type
              multiple: @is_multiple()
              thumb_url: URL.createObjectURL(file)
            }
            result = $.extend({}, file, properties)
            $template = @get_attachment_template(result).addClass('<%= SimpleFormAttachments.dom_class(:attachment, :is_uploading) %>')
            data.context = $template
            $template.data('data', data)
            @get_attachment_list().append($template)

          data.submit()

        fail: (e, data) =>
          @set_logs(e, data) if @options.debug
          if !!data.jqXHR.responseText
            $result = $(JSON.parse(data.jqXHR.responseText).html).addClass('<%= SimpleFormAttachments.dom_class(:attachment, :has_error) %>')
            $result.find('.<%= SimpleFormAttachments.dom_class(:attachment, :col, :errors) %>');
            data.context.replaceWith($result)

        progress: (e, data) =>
          @set_logs(e, data) if @options.debug
          progress = parseInt(data.loaded / data.total * 100, 10)
          data.context.find('.<%= SimpleFormAttachments.dom_class(:attachment, :col, :progress, :bar) %>').width(progress+'%')

        done: (e, data) =>
          @set_logs(e, data) if @options.debug
          console.log data.result.html if @options.debug
          $result = $(data.result.html).addClass('<%= SimpleFormAttachments.dom_class(:attachment, :is_uploaded) %>')
          data.context.replaceWith($result)

        stop: (e, data) =>
          @set_logs(e, data) if @options.debug
          @enable_form_submit()

      @$element.on 'click', '.<%= SimpleFormAttachments.dom_class(:button, :delete) %>, .<%= SimpleFormAttachments.dom_class(:button, :close) %>', (e) =>
        e.preventDefault()
        e.stopPropagation()
        $template = $(e.currentTarget).closest('.<%= SimpleFormAttachments.dom_class(:attachment) %>')
        @remove_template($template)

      @$element.on 'click', '.<%= SimpleFormAttachments.dom_class(:button, :cancel) %>', (e) =>
        e.preventDefault()
        e.stopPropagation()
        $template = $(e.currentTarget).closest('.<%= SimpleFormAttachments.dom_class(:attachment) %>')
        data = $template.data('data') || {}
        @enable_form_submit()

        if !!data.jqXHR
          data.jqXHR.abort()
        else
          data.errorThrown = 'abort'
          e.data.fileupload._trigger('fail', e, data)

        @remove_template($template)

      @$element.on 'dragover', (e) =>
        e.preventDefault()
        @$element.addClass('dragover')

      @$element.on 'dragleave', (e) =>
        e.preventDefault()
        @$element.removeClass('dragover')

      @$element.on 'drop', (e) =>
        e.preventDefault()
        @$element.removeClass('dragover')

    # ---------------------------------------------------------------------

    get_attachment_list: -> @$element.find('.<%= SimpleFormAttachments.dom_class(:attachment_list) %>')
    get_attachments_path: -> @$element.data('attachments-path')
    get_disabled_submit_text: -> @$element.data('disabled-submit-text')
    get_file_input: -> @$element.find('input.file')
    get_form: -> @$element.parents('form').first()
    get_form_submit: -> @get_form().find('input[type="submit"]').first()
    get_max_number_of_files: -> @$element.data('max-number-of-files')
    get_number_of_files: -> @get_attachment_list().children('.<%= SimpleFormAttachments.dom_class(:attachment) %>').not('.<%= SimpleFormAttachments.dom_class(:attachment, :has_error) %>').length

    get_attachment_type_input: -> @$element.find('input[name="attachment_type"]')
    get_attachment_multiple_input: -> @$element.find('input[name="attachment[multiple]"]')
    get_attachment_parent_class_input: -> @$element.find('input[name="attachment_parent[class]"]')
    get_attachment_parent_name_input: -> @$element.find('input[name="attachment_parent[name]"]')
    get_attachment_relation_key_input: -> @$element.find('input[name="attachment_relation[key]"]')
    get_attachment_relation_name_input: -> @$element.find('input[name="attachment_relation[name]"]')
    get_attachment_relation_type_input: -> @$element.find('input[name="attachment_relation[type]"]')
    get_attachment_relation_referenced_input: -> @$element.find('input[name="attachment_relation[referenced]"]')

    get_relation_name: -> @$element.data('relation-name')
    get_relation_key: -> @$element.data('relation-key')

    get_size_human: (bytes) -> numeral(bytes).format('0.0 b')

    get_attachment_template: (file) =>
      template = Handlebars.compile(@$element.find('script#simple_form_attachments__template').html())
      $(template(file))

    # ---------------------------------------------------------------------

    set_logs: (e, data) ->
      console.log e.type
      console.log e
      console.log data

    enable_form_submit: ->
      $submit = @get_form_submit()
      submit_text = $submit.data('submit-text')
      $submit.removeAttr('disabled')
      $submit.val(submit_text)

    disable_form_submit: ->
      $submit = @get_form_submit()
      $submit.attr('data-submit-text', $submit.val())
      $submit.val(@get_disabled_submit_text()).attr('disabled', 'disabled')

    remove_template: ($template) -> $template.slideUp(@options.slide_speed).promise().done -> $(this).remove()

    create_index: -> new Date().getTime() + @attachments_counter++

    # ---------------------------------------------------------------------

    is_multiple: -> @get_file_input().attr('multiple') != undefined
    is_sortable: -> @get_attachment_list().hasClass('<%= SimpleFormAttachments.dom_class(:attachment_list, :is_sortable) %>')

    # ---------------------------------------------------------------------

    set_unique_ids: ->
      $input = @get_file_input()
      default_id = $input.attr('id')
      $label = @$element.find("[for='#{default_id}']")
      $input.data('default-id', default_id)
      $label.data('default-for', default_id)
      new_id = "#{default_id}__#{@guid()}"
      $input.attr('id', new_id)
      $label.attr('for', new_id)

    # ---------------------------------------------------------------------

    guid: -> @s4() + @s4() + '-' + @s4() + '-' + @s4() + '-' + @s4() + '-' + @s4() + @s4() + @s4()
    s4: -> Math.floor((1 + Math.random()) * 0x10000).toString(16).substring(1)

  # ---------------------------------------------------------------------

  $.fn[pluginName] = (options) ->
    @each ->
      if !$.data(this, "plugin_#{pluginName}")
        $.data(@, "plugin_#{pluginName}", new Plugin(@, options))
)(jQuery, window)

$ -> $('div.simple_form_attachments').simple_form_attachments()
